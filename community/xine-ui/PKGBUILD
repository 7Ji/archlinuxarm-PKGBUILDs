# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: Eric BÃ©langer

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch from debian to fix ARM FTBFS

pkgname=xine-ui
pkgver=0.99.14
pkgrel=1
pkgdesc='A free multimedia player'
arch=('x86_64')
license=('GPL')
url='https://www.xine-project.org'
depends=('xine-lib' 'aalib' 'curl' 'hicolor-icon-theme' 'libjpeg-turbo' 'libpng'
         'libx11' 'libxext' 'libxft' 'libxinerama' 'libxv' 'libxxf86vm' 'lirc'
         'readline')
source=("https://downloads.sourceforge.net/xine/${pkgname}-${pkgver}.tar.xz"
        '0003-Fix-build.patch')
sha256sums=('d4d490d5cece70e2bb9849c9e482f2cf87af0302d451b614476fdcc3642cd9c3'
            'f4a99353034788321c13ac754dd2122dae21d1f902f05e4917036e39d8e8b60b')

prepare() {
    cd "${pkgname}-${pkgver}"
    patch -p1 -i ../0003-Fix-build.patch
    autoreconf -fi
}

build() {
    cd "${pkgname}-${pkgver}"
    ./configure --prefix='/usr' --with-x --without-caca
    make
}

package() {
    make -C "${pkgname}-${pkgver}" DESTDIR="$pkgdir" install
}
