# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - swap zstd for gzip in depends, remove gzip optdepend
#  - patch to revert back to gzip for default compression
#  - patch for gzip kernel image support (https://github.com/archlinux/mkinitcpio/pull/86)

pkgname=mkinitcpio
pkgver=35.1
pkgrel=1.1
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url='https://gitlab.archlinux.org/archlinux/mkinitcpio/mkinitcpio'
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'binutils' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'systemd')
makedepends=('asciidoc')
checkdepends=('bash-bats' 'bash-bats-assert' 'lzop')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('initramfs')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        '0001-use-gzip-for-compression-by-default.patch'
        '0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch'
        '0003-check-for-ALARM-kernels-in-run_post_hooks.patch')
install=mkinitcpio.install
sha512sums=('71dd4bcad75c60178e8f152568fbb70b37a983641d4953035aabcbff5bd49571182c9d92383f34ffc0078532674793790b181cf96c1654edb39b4ca94aefc7a4'
            'SKIP'
            '4205c5977d58728f6cdec22b553e000091e19b28e07c6099c14882842f964f66abf7b1b0a341e9b061e04c3c3ed1275647caf51d3770a61b60271712a8ecf74d'
            '693bddd2d3e3760479c6615085a6d7426cf2a6f92a42925192658eae1bcd66ae1bc46027321a2a5538abbb21025467c42d07569024f338b7bb6d4416e9922406'
            '4a89522a5350372ee189409529b3e5306a54520ce393eda511230c498ab6d56977e37c528904dcf01a667fa0602e5158bfe4834fd4205c835061cc8844e59b2d')
b2sums=('50783c27d7fad38ca3e02e1d8f15e42062b32e640ea26bc235ccd603659be5a2c218869fbd9e8491b3e0b027fa4613a599c7ce9768cd17c47e68053215a97f66'
        'SKIP'
        'ae8e25ab7e8d4cac52dcee7176bf132ea7cefeab67c3af69cecdf4871f50e42753dbe0c09a6fd20ef69c10a5b56bf6bc8f0614df1fd593d7a1ad3a5043f5c7b5'
        '8ca2d8902ae18402c18b3874c3977953a16f3ffaad760f21aafa7f15421a034bdd10fb58962ea5e15ace7407fa1f36343ddfcdca1bcd58ef12aded22ae2cabca'
        '20e32564bb0fafc2f742be0866f731e0a0fa0a4b009623564491d5248348e29c7d7864f97747d62298debfc341e72ba73f7393d449515db8d1630ecb36640b58')
validpgpkeys=('ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB'    # Giancarlo Razzolini
              'C100346676634E80C940FB9E9C02FF419FECBE16')   # Morten Linderud

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../0001-use-gzip-for-compression-by-default.patch
  patch -p1 -i ../0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch
  patch -p1 -i ../0003-check-for-ALARM-kernels-in-run_post_hooks.patch
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
