# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - swap zstd for gzip in depends, remove gzip optdepend
#  - patch to revert back to gzip for default compression
#  - patch for gzip kernel image support (https://github.com/archlinux/mkinitcpio/pull/86)

pkgname=mkinitcpio
pkgver=33
pkgrel=1
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url='https://github.com/archlinux/mkinitcpio'
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'binutils' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'systemd')
makedepends=('asciidoc')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('initramfs')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        '0001-use-gzip-for-compression-by-default.patch'
        '0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch')
install=mkinitcpio.install
sha512sums=('13526e749d23f2933db4a9ed74f138b0fafaf9b93f455dcf686b29b950247f0b62f8f6a091d7006c41642b4106bf8bcd666af03ec44a9f0ee7cf2b57b5714cf0'
            'SKIP'
            '711fa620fcd5685e07f19772626eb838f710e8eea8a47c33c48689935e57bfa60f6b420b70fdb351b8222d2fd5310859864be26f640c6d72c486f871dd61d48d'
            '9f5d0129c93823175b04ec0a42c82fa4cc8adffb8a836773b9b08bb7d28767b1a1810f44b78501f1c501d19abd84a6d9a87d30e0d3e95c8d76f56ee5210fd5f3')
b2sums=('1a85152e6ce465e419e87a1915860be83c198f4e7a6ecab44277a68f2f1ecb316c7fed6e8af93b0282b646c2de1a8057831668749788030eb3e5256eeb28979b'
        'SKIP'
        '5ce998c788e90abb22df2104f9cd58a5707fad2ebad21bf29b7e4e4d3f65cb99b74e3417b93c91c45c72569262403932c5f4af5ac5becf03a8ee2e4f38be2ebf'
        '8804b5b1594de02e54530f6f07fb8c41d89fef19f597dd34f5fb9bc04b601a466daeffe5474e22d7139b59f20a82ac255301b16bb623e4dc3db1406d3cda6301')
validpgpkeys=('ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB'    # Giancarlo Razzolini
              'C100346676634E80C940FB9E9C02FF419FECBE16')   # Morten Linderud

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../0001-use-gzip-for-compression-by-default.patch
  patch -p1 -i ../0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
